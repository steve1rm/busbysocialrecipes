def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

def logging = dependencyGroup {
    implementation 'com.jakewharton.timber:timber:4.5.1'
}

def unitTesting = dependencyGroup {
    testImplementation 'junit:junit:4.12'
    testImplementation ('com.nhaarman:mockito-kotlin:1.5.0', {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin_stdlib'})
}

def debugging = dependencyGroup {
    implementation 'com.facebook.stetho:stetho:1.5.0'
}

def reactive = dependencyGroup {
    implementation 'io.reactivex.rxjava2:rxjava:2.1.13'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
}

def dependencyInjection = dependencyGroup {
    def version = '2.14.1'

    implementation "com.google.dagger:dagger:$version"
    implementation "com.google.dagger:dagger-android-support:$version"
    kapt "com.google.dagger:dagger-compiler:$version"
    kapt "com.google.dagger:dagger-android-processor:$version"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    unitTesting()
    logging()
    debugging()
    reactive()
    dependencyInjection()
}


